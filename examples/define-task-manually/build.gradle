import com.linecorp.thrift.plugin.CompileThrift

/*
 * Based on jruyi/thrift-gradle-plugin/../examples/test1/build.gradle
 * Modified 2023 LINE Corporation
 *
 * Copyright 2023 LINE Corporation
 *
 * LINE Corporation licenses this file to you under the Apache License,
 * version 2.0 (the "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at:
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'com.google.osdetector' version '1.7.3'
    id 'com.linecorp.thrift-gradle-plugin' version '+'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.thrift:libthrift:0.14.1'
}

compileThrift {
    // Disable default compileThriftTask
    autoDetectPlugin false
}

tasks.register("customCompileThrift", CompileThrift) {
    thriftExecutable = "../../lib/thrift/0.17.0/thrift.${osdetector.classifier}"
    sourceItems.from(layout.projectDirectory.dir("src/main/thrift"))
    // Using custom build folder
    outputDir.set(layout.buildDirectory.dir("gen-gen-src"))
    generators.put('java', 'private-members')
}

// We need to set task dependency and sourceSets manually for custom task.
tasks.named("compileJava") {
    dependsOn("customCompileThrift")
}

sourceSets {
    main {
        java {
            srcDir layout.buildDirectory.dir("gen-gen-src")
        }
    }
}
